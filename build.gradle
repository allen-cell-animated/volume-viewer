plugins {
    id "com.dorongold.task-tree" version "1.3.1"
    id "com.moowork.node" version "1.2.0"
}

wrapper {
    gradleVersion "5.1"
    distributionType Wrapper.DistributionType.BIN
}

node {
    version = "10.15.0"
    npmVersion = "6.7.0"
    download = true
    nodeModulesDir = file("${project.projectDir}")
}

ext {
    npmBin = "${projectDir}/node_modules/.bin"
}

task clean(type: NodeTask) {
    dependsOn npmInstall

    script = file("${npmBin}/rimraf")
    args = "es lib dist".tokenize()
}

task format(type: NodeTask) {
    dependsOn npmInstall

    script = file("${npmBin}/prettier")
    args = "--write src/**/*.ts".tokenize()
}

task lint(type: NodeTask) {
    dependsOn npmInstall

    script = file("${npmBin}/eslint")
    args = ["src/**/*.js"]
}


///////////////////////////
// Build
//////////////////////////
task bundle(type: NodeTask) {
    dependsOn npmInstall

    environment = [BABEL_ENV: "es", NODE_ENV: "production"]
    script = file("${npmBin}/webpack")
    args = "--config webpack.config.js".tokenize()
}

task devCSS(type: NodeTask) {
    dependsOn npmInstall

    script = file("${npmBin}/postcss")
    args = "src/style.css --output examples/style.css".tokenize()
}

task transpileES(type: NodeTask) {
    dependsOn npmInstall

    environment = [BABEL_ENV: "es"]
    script = file("${npmBin}/babel")
    args = "src --out-dir es --extensions .js --ignore src/**/*.test.ts".tokenize()
}

task build {
    dependsOn transpileES
}

task start(type: NodeTask) {
    dependsOn devCSS, bundle
    environment = [NODE_ENV: "dev"]

    script = file("${npmBin}/webpack-dev-server")
    args = "--config webpack.dev.js".tokenize()
}

ext {
    // Default version bump performed by `version` task. Can be overriden by commandline `-PreleaseType` flag.
    releaseType = project.properties["releaseType"] ?: "patch"
}

task version(type: NpmTask) {
    args = "version ${releaseType}".tokenize()
}